model {

#Priors
psi ~ dunif(0, 1)
p ~ dunif(0, 1)

B0.mean ~ dunif(-10,10)
tau.B0 <- 1/(sigma.B0*sigma.B0)
sigma.B0 ~ dunif(0,10)

B.DOS ~ dnorm(0, 0.6666667)
B.DOS2 ~ dnorm(0, 0.6666667)

P.trans ~ dunif(1, 20) # Length of initial transmitter period when transmitter effect is applied.
B.trans ~ dnorm(0, 0.6666667) # Transmitter effect.

# Field veg effects #
B.hierbas ~ dnorm(0, 0.6666667)
B.hierba_ht ~ dnorm(0, 0.6666667)
B.arbusto ~ dnorm(0, 0.6666667)
B.pastos ~ dnorm(0, 0.6666667)
B.pasto_ht ~ dnorm(0, 0.6666667)
B.salsola ~ dnorm(0, 0.6666667)
B.salsola_ht ~ dnorm(0, 0.6666667)
B.arbusto_cv ~ dnorm(0, 0.6666667)

# Individual characteristics #
B.peso ~ dnorm(0, 0.6666667)

for(j in 1:nSite) {
  for(t in 1:nSeason) {
    B0[j, t] ~ dnorm(B0.mean, tau.B0)
  }
}
for(i in 1:nBird) {
  for(j in 1:nDOS) {
    within_period[i, j] <- max(0, P.trans - time_since_depl[i, j]) / (P.trans - time_since_depl[i, j])
    logit(S[i, j]) <- B0[SiteInd[i], SeasonInd[i]] + B.DOS*DOS[i, j] +
      B.DOS2*pow(DOS[i, j], 2) + B.trans * after_depl[i, j] * within_period[i, j] +
      B.hierba_ht * hierba_ht.z[i] + B.arbusto * arbusto.z[i] + B.pastos * pastos.z[i] +
      B.pasto_ht * pasto_ht.z[i] + B.salsola * salsola.z[i] + B.salsola_ht * salsola_ht.z[i] +
      B.arbusto_cv * arbusto_cv.z[i] + B.peso * peso.z[i, j]
    }
  z[i, (first[i] + 1)] ~ dbern(S[i, (first[i] + 1)])
  for(j in (first[i] + 2):last[i]) {
    z[i, j] ~ dbern(S[i, j]*z[i, (j - 1)])
    }
  for(j in (first[i] + 1):last[i]) {
    Y.alive[i, j] ~ dbern(p * z[i, j])
    Y.dead[i, j] ~ dbern(psi * (1 - z[i, j]))
    }
  }
}
